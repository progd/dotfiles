set encoding=utf-8
scriptencoding utf-8

" Initialize {{{
let s:is_windows = has('win16') || has('win32') || has('win64')
let s:is_cygwin = has('win32unix')
function! IsWindows()
  return s:is_windows
endfunction

function! IsMac()
  return !s:is_windows && !s:is_cygwin
        \ && (has('mac') || has('macunix') || has('gui_macvim') ||
        \   (!executable('xdg-open') &&
        \     system('uname') =~? '^darwin'))
endfunction

if &compatible
  set nocompatible
endif

augroup vimrc
  autocmd!
augroup END

if IsWindows() && !exists('g:added_windows_git_path')
  let g:added_windows_git_path = 1
  " Git for Windows を優先する
  let $PATH = 'C:\Program Files\Git\bin;C:\Program Files\Git\usr\bin;' . $PATH
endif
" }}}

" dein.vim configure {{{
set runtimepath+=~/.vim_dein/repos/github.com/Shougo/dein.vim

call dein#begin(expand('~/.vim_dein'))

call dein#load_toml('~/.vim/dein.toml')
call dein#load_toml('~/.vim/dein_lazy.toml', {'lazy' : 1})

call dein#end()

filetype plugin indent on
syntax enable

if dein#check_install()
  call dein#install()
endif
" }}}

" General settings {{{
let g:mapleader = ","

" タブ・インデント設定
set tabstop=4
set shiftwidth=4
set expandtab
set autoindent
set cindent

" 検索
set ignorecase
set smartcase
set incsearch
set hlsearch

set visualbell
set t_vb = ""

" 自動改行しない
set textwidth=0

" バックスペースでインデントや改行を削除できるようにする
set backspace=indent,eol,start

let s:my_tmp_dir = expand("~/.vim_tmp")
if !isdirectory(s:my_tmp_dir)
  call mkdir(s:my_tmp_dir)
endif

set swapfile
let &directory=s:my_tmp_dir

set backup
let &backupdir=s:my_tmp_dir

set undofile
let &undodir=s:my_tmp_dir

if executable('jvgrep')
  set grepprg=jvgrep
endif
" }}}

" Appearance {{{
set background=dark

" 対応する括弧を強調
set showmatch

" カーソル行を強調表示
"autocmd vimrc BufEnter * set cursorline
"highlight CursorLine cterm=NONE ctermbg=DarkMagenta guibg=DarkMagenta

" 行番号を表示
set number

" ステータスライン
set laststatus=2

" タブライン
set showtabline=2

set foldcolumn=5

" 不可視文字を表示
set list
set listchars=tab:»-,trail:-,extends:»,precedes:«,nbsp:%
autocmd vimrc ColorScheme * highlight NonText cterm=NONE ctermfg=LightGray guifg=LightGray
autocmd vimrc ColorScheme * highlight SpecialKey cterm=NONE ctermfg=LightGray guifg=LightGray
" }}}

" Keymaps {{{
" emacs-like keybind {{{
noremap! <C-a>  <Home>
noremap! <C-e>  <End>
noremap! <C-b>  <Left>
noremap! <C-f>  <Right>
noremap! <C-d>  <Del>
noremap! <M-b>  <C-Left>
noremap! <M-f>  <C-Right>
noremap! <M-d>  <Del>
noremap! <C-g>  <Esc>
cnoremap <C-p>  <Up>
cnoremap <C-n>  <Down>
" }}}

" edit {{{
noremap L $
noremap H ^
nnoremap <leader>p "*p
nnoremap <A-v> $v^
" }}}

" tabs and windows {{{
nnoremap [tabwin] <Nop>
nmap     s [tabwin]
" window move-cursor
nnoremap          [tabwin]j     <C-w>j
nnoremap          [tabwin]k     <C-w>k
nnoremap          [tabwin]l     <C-w>l
nnoremap          [tabwin]h     <C-w>h
nnoremap          [tabwin]w     <C-w>w
" window moving
nnoremap          [tabwin]J     <C-w>J
nnoremap          [tabwin]K     <C-w>K
nnoremap          [tabwin]L     <C-w>L
nnoremap          [tabwin]H     <C-w>H
nnoremap          [tabwin]r     <C-w>r
" window resizing
nnoremap          [tabwin]=     <C-w>=
" window manipulating
nnoremap          [tabwin]o     <C-w>o
nnoremap <silent> [tabwin]s     :<C-u>split<CR>
nnoremap <silent> [tabwin]v     :<C-u>vsplit<CR>
nnoremap          [tabwin]T     <C-w>T
" tabpage changing
nnoremap          <C-TAB>       gt
nnoremap          <C-S-TAB>     gT
nnoremap          [tabwin]n     gt
nnoremap          [tabwin]p     gT
nnoremap <silent> [tabwin]t     :<C-u>tabnew<CR>
" tabpage moving
nnoremap <silent> [tabwin]v     :<C-u>tabmove<CR>
nnoremap <silent> [tabwin]N     :<C-u>tabmove +1<CR>
nnoremap <silent> [tabwin]P     :<C-u>tabmove -1<CR>
" misc
nnoremap <silent> [tabwin]q     :<C-u>q<CR>
nnoremap <silent> [tabwin]Q     :<C-u>q!<CR>
nnoremap <silent> [tabwin]d     :<C-u>bd<CR>
nnoremap <silent> [tabwin]D     :<C-u>bd!<CR>
" }}}

" search
nnoremap <leader>/ /<C-u>\V
" }}}

" Commands {{{

" relativenumber をトグルする
function! RelativeToggle()
  if &relativenumber
    setlocal norelativenumber
  else
    setlocal relativenumber
  endif
endfunction
command! -nargs=0 RelativeToggle :call RelativeToggle()

" virtualedit をトグルする
function! VeditToggle()
  if &virtualedit ==? 'all'
    setlocal virtualedit=
  else
    setlocal virtualedit=all
  endif
endfunction
command! -nargs=0 VeditToggle :call VeditToggle()

" tab 幅を 2/4 でトグルする
function! TabWidthToggle()
  if &tabstop == 4
    setlocal tabstop=2 softtabstop=2 shiftwidth=2
  else
    setlocal tabstop=4 softtabstop=4 shiftwidth=4
  endif
endfunction
command! -nargs=0 TabWidthToggle :call TabWidthToggle()

" }}}

" Plugins {{{
if dein#tap('unite.vim') " {{{
  if executable('jvgrep')
    " file_rec/async
    let g:unite_source_rec_async_command =
          \ ['jvgrep', '-l', '--no-color', '-l', '']
    " grep
    let g:unite_source_grep_command = 'jvgrep'
    let g:unite_source_grep_default_opts = '--exclude ''\.git'''
    let g:unite_source_grep_recursive_opt = '-R'
  elseif executable('ag')
    let g:unite_source_rec_async_command =
          \ ['ag', '--follow', '--nocolor', '--nogroup',
          \  '--hidden', '-g', '']
    let g:unite_source_grep_command = 'ag'
    let g:unite_source_grep_default_opts =
          \ '-i --vimgrep --hidden --ignore ' .
          \ '''.hg'' --ignore ''.svn'' --ignore ''.git'' --ignore ''.bzr'''
    let g:unite_source_grep_recursive_opt = ''
  endif

  nnoremap          <leader>u :<C-u>Unite -no-split<Space>
  nnoremap <silent> <leader>f :<C-u>Unite buffer<CR>
  nnoremap <silent> <leader>F :<C-u>Unite -buffer-name=bufferlist -toggle -winwidth=40 -vertical -no-quit -keep-focus -no-start-insert buffer<CR>
  nnoremap <silent> <leader>b :<C-u>Unite bookmark<CR>
  nnoremap <silent> <leader>r :<C-u>UniteWithBufferDir file<CR>
  nnoremap <silent> <leader>R :<C-u>UniteWithBufferDir file_rec/async<CR>
  nnoremap <silent> <leader>c :<C-u>UniteResume<CR>
  nnoremap <silent> <leader>h :<C-u>Unite help<CR>
  nnoremap <silent> <leader>g :<C-u>UniteWithBufferDir grep<CR>
  nnoremap <silent> <leader>G :<C-u>Unite grep<CR>
  nnoremap <silent> <leader>l :<C-u>Unite line<CR>
endif " }}}

if dein#tap('unite-outline') " {{{
  nnoremap <silent> <leader>o  :<C-u>Unite -winwidth=40 -vertical -no-quit outline<CR>
endif " }}}

if dein#tap('neomru.vim') " {{{
  nnoremap <silent> <leader>m  :<C-u>Unite<Space>file_mru<CR>
endif " }}}

if dein#tap('vimfiler.vim') " {{{
  " netrw.vim を無効にする
  let g:loaded_netrwPlugin = 1
  " デフォルトのファイルエクスプローラにする
  let g:vimfiler_as_default_explorer = 1

  nnoremap <silent> <leader>e  :<C-u>VimFilerBufferDir -buffer-name=filer -find -no-split -quit<CR>
  nnoremap <silent> <leader>t  :<C-u>VimFilerBufferDir -buffer-name=tree -winwidth=40 -split -explorer -toggle -no-quit<CR>
endif " }}}

if dein#tap('neocomplete.vim') " {{{
  " Disable AutoComplPop.
  let g:acp_enableAtStartup = 0
  " Use neocomplete.
  let g:neocomplete#enable_at_startup = 1
  " Use smartcase.
  let g:neocomplete#enable_smart_case = 1
  " Set minimum syntax keyword length.
  let g:neocomplete#sources#syntax#min_keyword_length = 3
  let g:neocomplete#lock_buffer_name_pattern = '\*ku\*'

  " Define dictionary.
  let g:neocomplete#sources#dictionary#dictionaries = {
        \ 'default' : '',
        \ 'ruby' : $HOME.'/.neocomplete/dict/ruby.dict',
        \ }

  " Define keyword.
  if !exists('g:neocomplete#keyword_patterns')
    let g:neocomplete#keyword_patterns = {}
  endif
  let g:neocomplete#keyword_patterns['default'] = '\h\w*'

  " Plugin key-mappings.
  inoremap <expr><C-z>     neocomplete#undo_completion()
  inoremap <expr><C-l>     neocomplete#complete_common_string()

  " Recommended key-mappings.
  " <CR>: close popup and save indent.
  inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
  function! s:my_cr_function()
    return (pumvisible() ? "\<C-y>" : "" ) . "\<CR>"
  endfunction
  " <TAB>: completion.
  inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
  " <C-h>, <BS>: close popup and delete backword char.
  inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
  inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"

  " Enable omni completion.
  autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
  autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
  autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
  autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
  autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags

  " Enable heavy omni completion.
  if !exists('g:neocomplete#sources#omni#input_patterns')
    let g:neocomplete#sources#omni#input_patterns = {}
  endif
  let g:neocomplete#sources#omni#input_patterns.php = '[^. \t]->\h\w*\|\h\w*::'
  let g:neocomplete#sources#omni#input_patterns.c = '[^.[:digit:] *\t]\%(\.\|->\)'
  let g:neocomplete#sources#omni#input_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'

  " For perlomni.vim setting.
  " https://github.com/c9s/perlomni.vim
  let g:neocomplete#sources#omni#input_patterns.perl = '\h\w*->\h\w*\|\h\w*::'

endif " }}}

if dein#tap('kwbdi.vim') " {{{
  nmap <leader>d <Plug>Kwbd
endif " }}}

if dein#tap('vim-quickrun') " {{{
  nnoremap <silent> <leader>q :QuickRun -outputter/buffer/split ":botright 8sp"<CR>
  let g:quickrun_config = {
        \   '_': {
        \     'runner' : 'vimproc',
        \     'runner/vimproc/updatetime' : 60
        \   },
        \   'markdown' : {
        \       'type': 'pandoc',
        \       'outputter': 'browser'
        \   },
        \ }
endif " }}}

if dein#tap('incsearch.vim') " {{{
  let g:incsearch#auto_nohlsearch = 0
  map /  <Plug>(incsearch-forward)
  map ?  <Plug>(incsearch-backward)
  map g/ <Plug>(incsearch-stay)
  map n  <Plug>(incsearch-nohl-n)
  map N  <Plug>(incsearch-nohl-N)
  map *  <Plug>(incsearch-nohl-*)
  map #  <Plug>(incsearch-nohl-#)
  map g* <Plug>(incsearch-nohl-g*)
  map g# <Plug>(incsearch-nohl-g#)
  nnoremap <silent> <Esc><Esc> :nohlsearch<CR>
endif " }}}

if dein#tap('vim-brightest') " {{{
  let g:brightest#highlight = { "group" : "BrightestUnderline" }
endif " }}}

if dein#is_sourced('lightline.vim') " {{{
  let g:lightline = {
        \ 'colorscheme': 'Tomorrow',
        \ }
  set noshowmode
endif " }}}

if dein#tap('qfixhowm') " {{{
  " キーマップリーダー
  let QFixHowm_Key = 'g'

  " howm_dirはファイルを保存したいディレクトリを設定
  let howm_dir             = '~/Documents/howm'
  let howm_filename        = '%Y-%m-%d/%Y-%m-%d-%H%M%S.txt'
  let howm_fileencoding    = 'utf8'
  let howm_fileformat      = 'dos'
  let QFixHowm_FoldingPattern = '^[=.*[]'

  let mygrepprg = 'grep'
endif " }}}

if dein#tap('vim-easy-align') " {{{
  " Start interactive EasyAlign in visual mode (e.g. vip,a)
  vmap <leader>a <Plug>(EasyAlign)
  " Start interactive EasyAlign for a motion/text object (e.g. ,aip)
  nmap <leader>a <Plug>(EasyAlign)
endif " }}}

if dein#tap('foldCC.vim') " {{{
  set foldtext=FoldCCtext()
  let g:foldCCtext_enable_autofdc_adjuster = 1
endif " }}}
" }}}

if has('gui_running') " {{{
  if dein#tap('vim-singleton')
    if has('clientserver')
      call singleton#enable()
    endif
  endif
endif " }}}
